plugins {
	id 'fabric-loom' version '0.11-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

static String terraformers() {
	def url = "https://maven.terraformersmc.com/"
	def conn = new URL(url).openConnection()
	conn.connect()
	url = conn.getResponseCode() == 521 ? "https://raw.githubusercontent.com/TerraformersMC/Archive/main/releases" : url
	return url
}

repositories {
	maven { url = terraformers() }
	maven { url = "https://storage.googleapis.com/devan-maven/" }
	maven { url = "https://api.modrinth.com/maven/" }
	maven { url = "https://maven.gegy.dev/" }
	maven { url = "https://quantumfusion.dev/maven/" }
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// ARRP. This is for generating a runtime resource pack (conditionally, as well)
	modImplementation "net.devtech:arrp:${project.arrp_version}"
	include "net.devtech:arrp:${project.arrp_version}"

	// Mod Menu because config screen access from another place
	modApi "com.terraformersmc:modmenu:${project.modmenu_version}"

	// Dashloader (compatibility)
	modCompileOnly "dev.quantumfusion.dashloader:dashloader-core:$project.dashloader_core_version"
	modCompileOnly "dev.quantumfusion.dashloader:dashloader-definitions:$project.dashloader_definitions_version"

	// a bit less load time
	modLocalRuntime "maven.modrinth:lazydfu:0.1.2"

	// UI library
	modImplementation "dev.lambdaurora:spruceui:${project.spruceui_version}"
	include "dev.lambdaurora:spruceui:${project.spruceui_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

loom {
	accessWidenerPath = file("src/main/resources/enhancedblockentities.accesswidener")
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// Select the repositories you want to publish to
	// To publish to maven local, no extra repositories are necessary. Just use the task `publishToMavenLocal`.
	repositories {
		// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	}
}
